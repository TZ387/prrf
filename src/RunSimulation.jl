module RunSimulation

using ..GridSetup
using ..RFSolver
using ..HeatSolver
using ..Timelapse
using ..PlottingAndVisualization

export run_simulation, run_heat_simulation

function run_simulation(grid_params, rf_params, boundary_conditions)

    n_cells = grid_params.nx * grid_params.ny * grid_params.nz
    @info "Setting up grid ($(grid_params.nx)×$(grid_params.ny)×$(grid_params.nz), $n_cells cells)..."
    grid = GridSetup.setup_grid(grid_params)

    @info "Solving RF problem..."
    V_dof, dh, cellValues = solve_rf(grid, rf_params, grid_params, boundary_conditions)

    @info "Computing electric field and thermal dissipation..."
    Qel, E_mag, E_vec = calculate_fields(cellValues, dh, V_dof, rf_params.sigma, grid_params)
    V = convert_V(V_dof, grid_params)

    return grid, V_dof, Qel, E_mag, E_vec, V
end

function run_heat_simulation(Qel, grid_params, heat_params;
                        create_timelapse::Bool = false)

    if create_timelapse
        @info "Running heat simulation with live plot..."
        return Timelapse.run_heat_timelapse(Qel, heat_params, grid_params)
    end

    @info "Running heat simulation (no live plot)..."
    Qzero = zeros(Float64, grid_params.nx, grid_params.ny, grid_params.nz)
    T = fill(heat_params.T_initial, grid_params.nx, grid_params.ny, grid_params.nz)

    for (phase_idx, (state, duration)) in enumerate(heat_params.schedule)
        state in (:on, :off) || error(
            "Unknown phase state $(repr(state)) in schedule entry $phase_idx. " *
            "Expected :on or :off.")

        Q_src = state == :on ? Qel : Qzero
        label = state == :on ? "heating" : "cooling"

        @info "Phase $phase_idx/$(length(heat_params.schedule)): $label for $(duration) s"
        T = HeatSolver.solve_heat_phase(T, Q_src, heat_params, grid_params, duration)
    end

    return T
end

end  # module RunSimulation